// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * The "PC" chip that represents the PC (program counter) that contains all the memory address which are instructions to be executed. Once an instruction is fetched, the counter is incremented.
 *
 * if (reset[t] == 1):
 *   out[t + 1] = 0;
 * elif (load[t] == 1):
 *   out[t + 1] = in[t];
 * elif (inc[t] == 1):
 *   out[t + 1] = out[t] + 1;
 * else:
 *   out[t + 1] = out[t];
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=preOut, out=incOut);
    Mux16(a=preOut, b=incOut, sel=inc, out=firstOut);
    Mux16(a=firstOut, b=in, sel=load, out=secondOut);
    Mux16(a=secondOut, b=false, sel=reset, out=thirdOut);
    Register(in=thirdOut, load=true, out=out, out=preOut);
}
